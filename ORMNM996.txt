      ******************************************************************
      *           SIORM - SISTEMA ORQUESTRADOR DE MENSAGENS            *
      *----------------------------------------------------------------*
      * OBJETIVO: LER NA FILA AS MENSAGENS CRIPTOGRAFADAS E            *
      *           E REALIZAR A DECRIPTOGRAFIA E APOS ENCAMINHAR        *
      *           AS MENSAGENS PARA O TRATAMENTO NEGOCIAL  E
      *           PARA O LOG DE SEGURANCA
      *
      * Erros      :  0 - SemErro          value 0.
      *               1 - ErroSimples (O erro pode nao ocorre na proxi-
      *                                ma mensagem. Pode continuar)
      *
      *               2 - ErroGrave   (Parar o processamento)
      *               4 - Ocorreu um erro mas a mensagem foi decifrada.
      *                   Eh um aviso.
      *
      ******************************************************************
       IDENTIFICATION DIVISION.
       PROGRAM-ID. ORMNM996.
       AUTHOR. CEDESBR662.
      *----------------------------------------------------------------*
       ENVIRONMENT DIVISION.
       CONFIGURATION SECTION.
       SOURCE-COMPUTER. IBM-3090.
       SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA.
      *----------------------------------------------------------------*
       DATA DIVISION.
       WORKING-STORAGE SECTION.
      *----------------------------------------------------------------*
       77 WS-RETORNO                     PIC 9(05) VALUE ZEROS.
       77 WS-TAM-CAB-SEGURANCA           PIC S9(4) USAGE COMP VALUE +0.
       77 WS-SYSID                       PIC X(04) VALUE SPACES.
       77 RESP1                          PIC 9(05) VALUE 0.             00004900
       77 RESP2                          PIC 9(05) VALUE 0.             00005000
       77 WS-ABEND-CODE                  PIC X(04) VALUE SPACES.
       77 WS-ABEND-PROGRAM               PIC X(08) VALUE SPACES.
       77 WS-NO-FILA-LEITURA             PIC X(50) VALUE SPACES.
       77 WS-HCONN-LEITURA               PIC S9(9) BINARY VALUE +0.
       77 WS-Q-HANDLE-LEITURA            PIC S9(9) BINARY VALUE +0.
       77 WS-TP-TRIGGER-LEITURA          PIC X(01) VALUE SPACES.
       77 WS-FILLER-1                    PIC X(600) VALUE SPACES.
       77 WS-FILLER-2                    PIC X(600) VALUE SPACES.
       77 WS-FILLER-1-TAM                PIC S9(9) COMP VALUE ZERO.
       77 WS-FILLER-2-TAM                PIC S9(9) COMP VALUE ZERO.
       77 WS-NO-FILA-GARANTIACOM         PIC X(50) VALUE SPACES.
       77 WS-HCONN-GARANTIACOM           PIC S9(9) BINARY VALUE +0.
       77 WS-Q-HANDLE-GARANTIACOM        PIC S9(9) BINARY VALUE +0.
       77 WS-NO-FILA-GARANTIASEM         PIC X(50) VALUE SPACES.
       77 WS-HCONN-GARANTIASEM           PIC S9(9) BINARY VALUE +0.
       77 WS-Q-HANDLE-GARANTIASEM        PIC S9(9) BINARY VALUE +0.
       77 WS-NO-FILA-LOGSEG              PIC X(50) VALUE SPACES.
       77 WS-HCONN-LOGSEG                PIC S9(9) BINARY VALUE +0.
       77 WS-Q-HANDLE-LOGSEG             PIC S9(9) BINARY VALUE +0.
       77 WS-NO-FILA-LOGSEG-BD           PIC X(50) VALUE SPACES.
       77 WS-HCONN-LOGSEG-BD             PIC S9(9) BINARY VALUE +0.
       77 WS-Q-HANDLE-LOGSEG-BD          PIC S9(9) BINARY VALUE +0.
       77 WS-NO-FILA-ERRODECRIPTO        PIC X(50) VALUE SPACES.
       77 WS-HCONN-ERRODECRIPTO          PIC S9(9) BINARY VALUE +0.
       77 WS-Q-HANDLE-ERRODECRIPTO       PIC S9(9) BINARY VALUE +0.
       77 WS-TAM-GETMAIN-PT              PIC S9(8) COMP VALUE +2162688.
       77 WS-REG-MQ-TAM                  PIC S9(8) COMP VALUE +1.
       77 WS-REG-MQ-TAM-ANT              PIC S9(8) COMP VALUE +0.
       77 WS-TAM-MSG-DECRIPTO            PIC S9(8) COMP VALUE +1.
       77 WS-TAM-MSG-EBCDIC              PIC S9(8) COMP VALUE +1.
       77 WS-CURRENT-DATE-FIELDS-I       PIC X(21).
       77 WS-CURRENT-DATE-FIELDS-F       PIC X(21).
       77 WS-TAM-MSG-CRIPTO-D            PIC 9(09) VALUE ZEROS.
       77 WS-POS                         PIC S9(8) COMP VALUE +0.
       77 WS-ORI-MSG-REC-ANT             PIC X(01) VALUE SPACES.
      *
       01 WS-CURRENT-DATA-HORA.
          03 WS-DATA-HORA.
             05 WS-CURRENT-DATA          PIC 9(08).
             05 FILLER REDEFINES WS-CURRENT-DATA.
                10 WS-CURRENT-AA         PIC 9(04).
                10 WS-CURRENT-MM         PIC 9(02).
                10 WS-CURRENT-DD         PIC 9(02).
             05 WS-CURRENT-HORA.
                10 WS-CURRENT-HR         PIC 9(02).
                10 WS-CURRENT-MN         PIC 9(02).
                10 WS-CURRENT-SS         PIC 9(02).
                10 WS-CURRENT-MS         PIC 9(02).
          03 WS-DIFF-GMT                 PIC S9(4).
      *
       01 WS-CURRENT-DTHR-FORMATADA      PIC X(26).
       01 WS-DT-IN                       PIC 9(08).
       01 WS-DT-INTEGER                  PIC S9(9) BINARY.
      *
       01 WS-INF-MSG-REC.
          03 WS-ORI-MSG-REC              PIC X(01) VALUE SPACES.
          03 WS-IDENTIFICADOR-MSG-REC    PIC X(24) VALUE SPACES.
          03 WS-CODMSG-REC-TAM           PIC 9(02) VALUE ZEROS.
          03 WS-CODMSG-REC               PIC X(09) VALUE SPACES.
          03 WS-DTHR-HR-PUT-GET          PIC X(26).
          03 FILLER REDEFINES WS-DTHR-HR-PUT-GET.
             05 WS-DATA-PUT-GET.
                07 WS-AAAA-PUT-GET       PIC 9(04).
                07 WS-MM-PUT-GET         PIC 9(02).
                07 WS-DD-PUT-GET         PIC 9(02).
             05 WS-HORA-PUT.
                07 WS-HR-PUT             PIC 9(02).
                07 WS-MM-PUT             PIC 9(02).
                07 WS-SS-PUT             PIC 9(02).
                07 WS-CS-PUT             PIC 9(02).
             05 WS-HORA-GET.
                07 WS-HR-GET             PIC 9(02).
                07 WS-MM-GET             PIC 9(02).
                07 WS-SS-GET             PIC 9(02).
                07 WS-CS-GET             PIC 9(02).
             05 FILLER                   PIC X(02).
      *
       01 WS-IDENT-MSG.
          03 WS-MSGID-MSG                PIC X(24) VALUE SPACES.
          03 WS-CORRELID-MSG             PIC X(24) VALUE SPACES.
          03 WS-IDENT-ORIG-MSG           PIC X(14) VALUE SPACES.
          03 WS-IDENT-DEST-MSG           PIC X(14) VALUE SPACES.

       01 WS-ERRO-DECRIPTO.
          03 WS-TS-DECRI-MSG             PIC X(26) VALUE SPACES.
          03 WS-RETORNO-ACAO-MSG         PIC S9(5) COMP VALUE 0.
          03 WS-RAZAO-ACAO-MSG           PIC S9(5) COMP VALUE 0.
          03 WS-DESC-ERRO-DECRI-MSG      PIC X(100) VALUE SPACES.

       01 WS-INF-MSG-ORIG.
          03 WS-ID-UNICO-DECRI.
             05 WS-TIPO-ID-UNICO         PIC X(03).
             05 WS-ID-UNICO-DECRI-MSG    PIC X(20) VALUE SPACES.
          03 WS-PUTDATE-MSG              PIC X(10) VALUE SPACES.
          03 WS-PUTTIME-MSG              PIC X(08) VALUE SPACES.
      **--------------------------------------------------------------**
       COPY ORMCP125.
       COPY ORMNM990.
       COPY ORMNM991.
       COPY ORMNM992.
       COPY ORMCPCBC.
       COPY ORMCPCNV.
       COPY ORMCPUID.
      **--------------------------------------------------------------**
       LINKAGE SECTION.
      ******************************************************************
      *   DEFINIÇÃO DA MENSAGEM LIDA NAS FILAS DE ENTRADA E SAÍDA      *
      *----------------------------------------------------------------*
       01 WS-MSG-MQ-CRIPTO.
          03 WS-TAM-MSG-CRIPTO             PIC S9(09) USAGE COMP.
          03 WS-MSG-CIFRADA.
             05 WS-MSG-CRIPTO              PIC X
                OCCURS 1 TO 2162688 DEPENDING ON WS-TAM-MSG-CRIPTO.

       01 WS-REG-MQ.
          03 WS-REG-MQ-LEN          PIC S9(09) USAGE COMP.
          03 WS-REG-MQ-TXT.
             05 FILLER PIC X
                OCCURS 1 TO 2162688  DEPENDING ON WS-REG-MQ-TAM.

       01 WS-MSG-DECRIPTO.
          03 FILLER PIC X
             OCCURS 1 TO 2162688     DEPENDING ON WS-TAM-MSG-DECRIPTO.

       01 WS-MSG-EBCDIC.
          03 FILLER PIC X
             OCCURS 1 TO 2162688     DEPENDING ON WS-TAM-MSG-EBCDIC.
      ******************************************************************
       PROCEDURE DIVISION.
      *----------------------------------------------------------------*
      *    Procedimentos iniciais
           PERFORM P0000-INICIALIZAR

      *    Executa O LOOP na fila de leitura
           PERFORM P0100-LOOP-LEITURA-MQ

      *    Limpo a memória
           PERFORM P1300-FREE-MAIN

      *    Finalizo o programa
           EXEC CICS RETURN END-EXEC
           .
      ******************************************************************
       P0000-INICIALIZAR.
      **--------------------------------------------------------------**
      *    Handle conditions
           EXEC CICS HANDLE ABEND LABEL(P9000-ABEND) END-EXEC
      *    Monta as filas associadas ao processo
           MOVE 'LQ.INT.SIORM.SEGURANCA.ERRO' TO WS-NO-FILA-ERRODECRIPTO
           MOVE 'LQ.INT.SIORM.SEGURANCA.LOG'  TO WS-NO-FILA-LOGSEG
           MOVE 'LQ.INT.SIORM.SEGURANCA.LOG.BD' TO WS-NO-FILA-LOGSEG-BD
           MOVE 'LQ.INT.SIORM.GARANTIA.COM'   TO WS-NO-FILA-GARANTIACOM
           MOVE 'LQ.INT.SIORM.GARANTIA.SEM'   TO WS-NO-FILA-GARANTIASEM
      *    Vincula a transação a fila de leitura associada
           EVALUATE EIBTRNID
             WHEN 'OQCL'
               MOVE 'LQ.INT.SIORM.LEGADO'     TO WS-NO-FILA-LEITURA
               MOVE 'L'                       TO WS-ORI-MSG-REC
               MOVE SPACES                    TO WS-TIPO-ID-UNICO
             WHEN 'OQCR'
               MOVE 'LQ.INT.SIORM.RSFN'       TO WS-NO-FILA-LEITURA
               MOVE 'R'                       TO WS-ORI-MSG-REC
               MOVE 'DEC'                     TO WS-TIPO-ID-UNICO
             WHEN 'OQCM'
               MOVE 'LQ.INT.SIORM.LEGADO.SPB02' TO WS-NO-FILA-LEITURA
               MOVE 'L'                       TO WS-ORI-MSG-REC
               MOVE SPACES                    TO WS-TIPO-ID-UNICO
             WHEN 'OQCN'
               MOVE 'LQ.INT.SIORM.RSFN.SPB02' TO WS-NO-FILA-LEITURA
               MOVE 'R'                       TO WS-ORI-MSG-REC
               MOVE 'DEC'                     TO WS-TIPO-ID-UNICO
      *      Somente em desenvolvimento
             WHEN 'OQCB'
               MOVE 'LQ.INT.SIORM.BROKER.NM.REC' TO WS-NO-FILA-LEITURA
               MOVE 'R'                       TO WS-ORI-MSG-REC
               MOVE 'BRK'                     TO WS-TIPO-ID-UNICO
             WHEN OTHER
               STRING 'ORMNM996 - TASK DESCONHECIDA: ' EIBTRNID
                 DELIMITED BY SIZE INTO LK-ASSUNTO-EMAIL-125
               END-STRING
               MOVE 99999                     TO WS-RETORNO
               PERFORM P9200-EXE-ERRO
           END-EVALUATE

      *    Abro  fila de onde serão lidas as mensagens criptografadas
      *    conforme o nome da transação vinculada
           INITIALIZE                          LINKAGE-ORMPO990
           MOVE 'G'                         TO LK-IC-TIPO-OPEN-990
           MOVE WS-NO-FILA-LEITURA          TO LK-SOURCE-QUEUE-990
           PERFORM P1000-ABRIR-FILA
           MOVE LK-HCONN-990                TO WS-HCONN-LEITURA
           MOVE LK-Q-HANDLE-990             TO WS-Q-HANDLE-LEITURA
           MOVE LK-TP-TRIGGER-990           TO WS-TP-TRIGGER-LEITURA

      *    Abro as filas para a entrega das mensangen após o processo
      *    de decriptografia
           INITIALIZE                          LINKAGE-ORMPO990
           MOVE 'P'                         TO LK-IC-TIPO-OPEN-990
           MOVE WS-NO-FILA-GARANTIACOM      TO LK-SOURCE-QUEUE-990
           PERFORM P1000-ABRIR-FILA
           MOVE LK-HCONN-990                TO WS-HCONN-GARANTIACOM
           MOVE LK-Q-HANDLE-990             TO WS-Q-HANDLE-GARANTIACOM
      *
           INITIALIZE                          LINKAGE-ORMPO990
           MOVE 'P'                         TO LK-IC-TIPO-OPEN-990
           MOVE WS-NO-FILA-GARANTIASEM      TO LK-SOURCE-QUEUE-990
           PERFORM P1000-ABRIR-FILA
           MOVE LK-HCONN-990                TO WS-HCONN-GARANTIASEM
           MOVE LK-Q-HANDLE-990             TO WS-Q-HANDLE-GARANTIASEM
      *
           INITIALIZE                          LINKAGE-ORMPO990
           MOVE 'P'                         TO LK-IC-TIPO-OPEN-990
           MOVE WS-NO-FILA-LOGSEG           TO LK-SOURCE-QUEUE-990
           PERFORM P1000-ABRIR-FILA
           MOVE LK-HCONN-990                TO WS-HCONN-LOGSEG
           MOVE LK-Q-HANDLE-990             TO WS-Q-HANDLE-LOGSEG
      *
           INITIALIZE                          LINKAGE-ORMPO990
           MOVE 'P'                         TO LK-IC-TIPO-OPEN-990
           MOVE WS-NO-FILA-LOGSEG-BD        TO LK-SOURCE-QUEUE-990
           PERFORM P1000-ABRIR-FILA
           MOVE LK-HCONN-990                TO WS-HCONN-LOGSEG-BD
           MOVE LK-Q-HANDLE-990             TO WS-Q-HANDLE-LOGSEG-BD
      *
           INITIALIZE                          LINKAGE-ORMPO990
           MOVE 'P'                         TO LK-IC-TIPO-OPEN-990
           MOVE WS-NO-FILA-ERRODECRIPTO     TO LK-SOURCE-QUEUE-990
           PERFORM P1000-ABRIR-FILA
           MOVE LK-HCONN-990                TO WS-HCONN-ERRODECRIPTO
           MOVE LK-Q-HANDLE-990             TO WS-Q-HANDLE-ERRODECRIPTO

      *    Trata ponteiros para  entrega da mensagem
           EXEC CICS GETMAIN SET(LK-MSG-PTR-992)
                         FLENGTH(WS-TAM-GETMAIN-PT)
           END-EXEC
           SET ADDRESS OF WS-REG-MQ        TO LK-MSG-PTR-992
      *    Trata ponteiros para a conversao
           EXEC CICS GETMAIN SET(LK-DEST-POINTER-CNV)
                         FLENGTH(WS-TAM-GETMAIN-PT)
           END-EXEC
           SET ADDRESS OF WS-MSG-EBCDIC    TO LK-DEST-POINTER-CNV
           .
      ******************************************************************
       P0100-LOOP-LEITURA-MQ.
      **--------------------------------------------------------------**
      *    Recupera a mensagem que sera transferida para as
      *    demais filas
           PERFORM P1100-GET-FILA-LEITURA

      *    Executa a loop para transferir a mensagens
           PERFORM UNTIL LK-RETORNO-991 EQUAL 99999
      *      Get the current date in yyyymmdd format
             MOVE FUNCTION CURRENT-DATE  TO WS-CURRENT-DATE-FIELDS-I

      *      Realiza a validacao de seguranca
             PERFORM P0700-SEGURANCA
      *      Verifica se teve erros na decriptografia
      *      erros graves finaliza o programa na hora que constatou
      *      erros simples as mensagens são encaminhadas para o erro
      *      de decriptografia
             IF ( ErroSimplesCBC )
      *        Entrega mensagem para tratamento erro de decrito
               PERFORM P0300-ENVIA-ERRODECRIPTO
             ELSE
      *        Recupera o codigo da mensagem
               PERFORM P0900-RECUPERA-CODMSG
      *        Verifica codigo da mensagem para definir o tratamento
               EVALUATE WS-CODMSG-REC(1:WS-CODMSG-REC-TAM)
                 WHEN 'DDA0110'
                 WHEN 'DDA0110E'
                 WHEN 'DDA0110R1'
      *          Entrega mensagem decriptografada
                    PERFORM P0500-ENVIA-GARANTIASEM
                 WHEN OTHER
      *             Entrega mensagem decriptografada
                    PERFORM P0400-ENVIA-GARANTIACOM
               END-EVALUATE
             END-IF
      *      Comita a mensagens no MQ
             EXEC CICS SYNCPOINT END-EXEC
      *      Libero a memoria utilizada pela msg
             PERFORM P1310-FREE-MAIN-MSG

      *      Get the current date in yyyymmdd format
             MOVE FUNCTION CURRENT-DATE  TO WS-CURRENT-DATE-FIELDS-F

      *      Recupera a mensagem que sera transferida
             PERFORM P1100-GET-FILA-LEITURA
           END-PERFORM
           .
      ******************************************************************
       P0200-MSG-AVISO-DECRIPTO.
      **--------------------------------------------------------------**
      *    Se o retorno da decriptografia foi um aviso envio
      *    somente envio um e-mail e continuo tratando como
      *    senão houvesse problemas
           MOVE SPACES               TO LK-ASSUNTO-EMAIL-125
           STRING 'ORMNM996 -'
                  ' *** AVISO processo DECRIPTO ***'
                  ' Erro: ' LK-DESC-ERRO-CBC
                  ' Orig: ' LK-IDENT-ORIG-CBC
                  ' Dest: ' LK-IDENT-DEST-CBC
                  DELIMITED BY SIZE INTO LK-ASSUNTO-EMAIL-125
           END-STRING
           PERFORM P9100-ENVIA-EMAIL
           .
      ******************************************************************
       P0700-SEGURANCA.
      *----------------------------------------------------------------*
      *    Verifico o tipo de mensagem
           IF WS-MSG-CIFRADA  (1:6) EQUAL 'LEGADO'
              PERFORM P0701-PADRAOLEGADO
           ELSE
            IF WS-MSG-CIFRADA(1:12) EQUAL X'004300490050004E00500043'
              PERFORM P0701-PADRAOBROKER
            ELSE
      *       Recupero o tamanho do cabeçalho de segurança
               IF WS-MSG-CIFRADA(1:2) EQUAL X'024C'
                  MOVE 588                TO WS-TAM-CAB-SEGURANCA
               END-IF
               IF WS-MSG-CIFRADA(1:2) EQUAL X'014C'
                 MOVE 332                TO WS-TAM-CAB-SEGURANCA
               END-IF
               PERFORM P0701-PADRAORSFN
            END-IF
           END-IF
           .
      ******************************************************************
       P0300-ENVIA-ERRODECRIPTO.
      **--------------------------------------------------------------**
      *    Na decriptografia a mensagem deve ser logada
      *    na fila de erros de decriptografia

      *    Monto mensagem informando os erros simples de
      *    Decriptografia ou Criptografia

      *    Recupera o nome do CICS
           EXEC CICS ASSIGN SYSID(WS-SYSID) END-EXEC

           MOVE SPACES               TO LK-ASSUNTO-EMAIL-125
           STRING 'CICS: ' WS-SYSID
                  ' - TASK: ' EIBTRNID
                  ' - ORMNM996 -'
                  ' *** ERRO SIMPLES DECRIPTO ***'
                  ' Orig: ' LK-IDENT-ORIG-CBC
                  ' Dest: ' LK-IDENT-DEST-CBC
                  ' Erro: ' LK-DESC-ERRO-CBC
                  DELIMITED BY SIZE INTO LK-ASSUNTO-EMAIL-125
           END-STRING
           PERFORM P9100-ENVIA-EMAIL

      *    Recupera as informações do erro
           MOVE LK-RETORNO-ACAO-CBC        TO WS-RETORNO-ACAO-MSG
           MOVE LK-RAZAO-ACAO-CBC          TO WS-RAZAO-ACAO-MSG
           MOVE LK-IDENT-ORIG-CBC          TO WS-IDENT-ORIG-MSG
           MOVE LK-MSGID-991               TO WS-CORRELID-MSG
           MOVE LK-CORRELID-991            TO WS-MSGID-MSG
           MOVE LK-DESC-ERRO-CBC           TO WS-DESC-ERRO-DECRI-MSG

      *    Monto o registro que sera colocado no MQ
           MOVE LENGTH OF WS-IDENT-MSG TO WS-REG-MQ-TAM
           MOVE WS-IDENT-MSG           TO WS-REG-MQ-TXT
           COMPUTE WS-REG-MQ-TAM-ANT = WS-REG-MQ-TAM + 1

           COMPUTE WS-REG-MQ-TAM = WS-REG-MQ-TAM +
                                   LENGTH OF WS-ERRO-DECRIPTO
           MOVE WS-ERRO-DECRIPTO
             TO WS-REG-MQ-TXT(WS-REG-MQ-TAM-ANT:WS-REG-MQ-TAM)
           COMPUTE WS-REG-MQ-TAM-ANT = WS-REG-MQ-TAM + 1

           MOVE WS-TAM-MSG-CRIPTO          TO WS-TAM-MSG-CRIPTO-D
           COMPUTE WS-REG-MQ-TAM = WS-REG-MQ-TAM +
                                   LENGTH OF WS-TAM-MSG-CRIPTO-D
           MOVE WS-TAM-MSG-CRIPTO-D
             TO WS-REG-MQ-TXT(WS-REG-MQ-TAM-ANT:WS-REG-MQ-TAM)
           COMPUTE WS-REG-MQ-TAM-ANT = WS-REG-MQ-TAM + 1

           COMPUTE WS-REG-MQ-TAM = WS-REG-MQ-TAM + WS-TAM-MSG-CRIPTO
           MOVE WS-MSG-CIFRADA  (1:WS-TAM-MSG-CRIPTO)
             TO WS-REG-MQ-TXT(WS-REG-MQ-TAM-ANT:WS-TAM-MSG-CRIPTO)
           MOVE WS-REG-MQ-TAM            TO WS-REG-MQ-LEN

      *    Define os controles das mensagens e ponteiro correto da msg
           MOVE LK-MSGID-991             TO LK-MSGID-992
           MOVE LK-CORRELID-991          TO LK-CORRELID-992
      *    Envia a mensagem para fila associada que foi recuperada
           MOVE WS-HCONN-ERRODECRIPTO    TO LK-HCONN-992
           MOVE WS-Q-HANDLE-ERRODECRIPTO TO LK-Q-HANDLE-992
      *    Configuro o padrão do put da mensagem
           MOVE 'S'                      TO LK-PERSISTENCE-992
           MOVE ZEROS                    TO LK-EXPIRY-992
           MOVE SPACES                   TO LK-REPLY-QUEUE-992
           MOVE SPACES                   TO LK-REPLY-QMGR-992
           MOVE 1200                     TO LK-CODEDCHARSETID-992
           MOVE 785                      TO LK-ENCODING-992

           PERFORM P1200-PUT-MQ

      *    Monto mensagem informando que aconteceu erro de decripto
           STRING
               'ORMNM996 -'
               ' *** ERRO SIMPLES DECRIPTO ***'
               ' - Mensagem colocada na Fila de erro de processamento.'
               ' Data/Hora: ' WS-CURRENT-DATE-FIELDS-I
           DELIMITED BY SIZE INTO LK-ASSUNTO-EMAIL-125
           END-STRING
           PERFORM P9100-ENVIA-EMAIL

      *************** ATENÇÃO ATENÇÃO ATENÇÃO ATENÇÃO ***************
      *   A mensagem foi logada na fila de tratamento de erro, assim
      *   a mensagem deve ser saltada e passar para a proxima da fila
      *************** ATENÇÃO ATENÇÃO ATENÇÃO ATENÇÃO ***************
           .
      ******************************************************************
       P0400-ENVIA-GARANTIACOM.
      **--------------------------------------------------------------**
      *    Envia a mensagem para fila associada que foi recuperada
           MOVE WS-HCONN-GARANTIACOM     TO LK-HCONN-992
           MOVE WS-Q-HANDLE-GARANTIACOM  TO LK-Q-HANDLE-992
      *    Configuro o padrão do put da mensagem
           MOVE 'S'                      TO LK-PERSISTENCE-992
           MOVE ZEROS                    TO LK-EXPIRY-992
           MOVE SPACES                   TO LK-REPLY-QUEUE-992
           MOVE SPACES                   TO LK-REPLY-QMGR-992
           MOVE WS-ID-UNICO-DECRI-MSG    TO WS-IDENTIFICADOR-MSG-REC

           PERFORM P0400-ENVIA-TRATAMENTO
           .
      ******************************************************************
       P0500-ENVIA-GARANTIASEM.
      **--------------------------------------------------------------**
      *    Envia a mensagem para fila associada que foi recuperada
           MOVE WS-HCONN-GARANTIASEM           TO LK-HCONN-992
           MOVE WS-Q-HANDLE-GARANTIASEM        TO LK-Q-HANDLE-992
      *    Configuro o padrão do put da mensagem
           MOVE 'N'                            TO LK-PERSISTENCE-992
      *    Tempo em milisegundos 30 seg = 300
      *    MOVE 300                      TO LK-EXPIRY-992
           MOVE 30000                          TO LK-EXPIRY-992
           MOVE 'LQ.REP.SIORM.EXPIRY.MENSAGEM' TO LK-REPLY-QUEUE-992
           MOVE 'QM.00360305.01'               TO LK-REPLY-QMGR-992
           MOVE WS-ID-UNICO-DECRI        TO WS-IDENTIFICADOR-MSG-REC

           PERFORM P0400-ENVIA-TRATAMENTO
           .
      ******************************************************************
       P0400-ENVIA-TRATAMENTO.
      **--------------------------------------------------------------**

      *    Monto o registro que sera colocado no MQ (informacoes gerais)
           MOVE LENGTH OF WS-INF-MSG-REC TO WS-REG-MQ-TAM
           MOVE WS-INF-MSG-REC          TO WS-REG-MQ-TXT
           COMPUTE WS-REG-MQ-TAM-ANT = WS-REG-MQ-TAM + 1

           COMPUTE WS-REG-MQ-TAM = WS-REG-MQ-TAM + WS-TAM-MSG-EBCDIC
           MOVE WS-MSG-EBCDIC(1:WS-TAM-MSG-EBCDIC)
             TO WS-REG-MQ-TXT(WS-REG-MQ-TAM-ANT:WS-REG-MQ-TAM)
           MOVE WS-REG-MQ-TAM            TO WS-REG-MQ-LEN

      *    Define os controles das mensagens e ponteiro correto da msg
           MOVE WS-ID-UNICO-DECRI        TO LK-MSGID-992
           MOVE WS-ID-UNICO-DECRI        TO LK-CORRELID-992
           MOVE 500                      TO LK-CODEDCHARSETID-992
           MOVE 785                      TO LK-ENCODING-992

           PERFORM P1200-PUT-MQ

           MOVE WS-ORI-MSG-REC-ANT       TO WS-ORI-MSG-REC
           .
      ******************************************************************
       P0600-ENVIA-LOGSEG.
      **--------------------------------------------------------------**
      *    Recupero as informações de controle para poder vincular
      *    a mensagem decriptografada com o cabeçalho de segurança
      *    da mensagem criptografada.
      **--------------------------------------------------------------**
      * Monto WS-IDENT-MSG
           MOVE LK-MSGID-991             TO WS-MSGID-MSG
           MOVE LK-CORRELID-991          TO WS-CORRELID-MSG
           MOVE LK-IDENT-ORIG-CBC        TO WS-IDENT-ORIG-MSG
           MOVE LK-IDENT-DEST-CBC        TO WS-IDENT-DEST-MSG

      * Monto WS-INF-MSG-ORIG
           MOVE LK-MSGID-UNICO-CBC       TO WS-ID-UNICO-DECRI-MSG
           MOVE LK-PUTDATE-991           TO WS-PUTDATE-MSG
           MOVE LK-PUTTIME-991           TO WS-PUTTIME-MSG

      * Monto o registro que sera colocado no MQ
           MOVE 1                        TO WS-POS
           MOVE LENGTH OF WS-IDENT-MSG   TO WS-REG-MQ-TAM
           MOVE WS-IDENT-MSG
             TO WS-REG-MQ-TXT(WS-POS:WS-REG-MQ-TAM)

           COMPUTE WS-POS = WS-REG-MQ-TAM + 1
           COMPUTE WS-REG-MQ-TAM = WS-REG-MQ-TAM +
                                   LENGTH OF WS-INF-MSG-ORIG
           MOVE WS-INF-MSG-ORIG
             TO WS-REG-MQ-TXT(WS-POS:WS-REG-MQ-TAM)

           COMPUTE WS-POS = WS-REG-MQ-TAM + 1
           COMPUTE WS-REG-MQ-TAM = WS-REG-MQ-TAM + WS-TAM-CAB-SEGURANCA

           MOVE WS-MSG-CIFRADA (1:WS-TAM-CAB-SEGURANCA)
             TO WS-REG-MQ-TXT(WS-POS:WS-REG-MQ-TAM)

           COMPUTE WS-POS = WS-REG-MQ-TAM + 1
           COMPUTE WS-REG-MQ-TAM = WS-REG-MQ-TAM + WS-TAM-MSG-DECRIPTO

           MOVE WS-MSG-DECRIPTO(1:WS-TAM-MSG-DECRIPTO)
             TO WS-REG-MQ-TXT(WS-POS:WS-REG-MQ-TAM)

           COMPUTE WS-REG-MQ-LEN = LENGTH OF WS-REG-MQ-LEN
                                 + WS-REG-MQ-TAM

      *    Define os controles das mensagens e ponteiro correto da msg
           MOVE WS-ID-UNICO-DECRI        TO LK-MSGID-992
           MOVE WS-ID-UNICO-DECRI        TO LK-CORRELID-992
      *    Envia a mensagem para fila associada que foi recuperada
           MOVE WS-HCONN-LOGSEG          TO LK-HCONN-992
           MOVE WS-Q-HANDLE-LOGSEG       TO LK-Q-HANDLE-992
      *    Configuro o padrão do put da mensagem
           MOVE 'S'                      TO LK-PERSISTENCE-992
           MOVE ZEROS                    TO LK-EXPIRY-992
           MOVE SPACES                   TO LK-REPLY-QUEUE-992
           MOVE SPACES                   TO LK-REPLY-QMGR-992
           MOVE 1200                     TO LK-CODEDCHARSETID-992
           MOVE 785                      TO LK-ENCODING-992

           PERFORM P1200-PUT-MQ

      *    Define os controles das mensagens e ponteiro correto da msg
           MOVE WS-ID-UNICO-DECRI        TO LK-MSGID-992
           MOVE WS-ID-UNICO-DECRI        TO LK-CORRELID-992
      *    Envia a mensagem para fila associada que foi recuperada
           MOVE WS-HCONN-LOGSEG-BD       TO LK-HCONN-992
           MOVE WS-Q-HANDLE-LOGSEG-BD    TO LK-Q-HANDLE-992
      *    Configuro o padrão do put da mensagem
           MOVE 'S'                      TO LK-PERSISTENCE-992
           MOVE ZEROS                    TO LK-EXPIRY-992
           MOVE SPACES                   TO LK-REPLY-QUEUE-992
           MOVE SPACES                   TO LK-REPLY-QMGR-992
           MOVE 1200                     TO LK-CODEDCHARSETID-992
           MOVE 785                      TO LK-ENCODING-992

           PERFORM P1200-PUT-MQ
           .
      ******************************************************************
       P0701-PADRAORSFN.
      *----------------------------------------------------------------*
      *    Realiza a decroptografia da mensagem
           PERFORM P0702-DECRIPTOGRAFIA
           IF ( NOT ErroSimplesCBC )THEN
      *      Verifica se tem um aviso na decriptografia
             IF (AvisoCBC) THEN
      *         Envia a aviso de alerta da decriptografia
                PERFORM P0200-MSG-AVISO-DECRIPTO
             END-IF
      *      Endereco que ira receber as mensagens
             SET ADDRESS OF WS-MSG-DECRIPTO  TO LK-MSG-SAI-CBC
             COMPUTE WS-TAM-MSG-DECRIPTO = LK-TAM-MSG-SAI-CBC
      *      Entrega mensagem decriptografada
             PERFORM P0600-ENVIA-LOGSEG
      *      Convert a mensagem para EBCDIC
             PERFORM P0800-CONVERSAO-CARACT
           END-IF
           .
      ******************************************************************
       P0701-PADRAOLEGADO.
      *----------------------------------------------------------------*
           MOVE 332                  TO WS-TAM-CAB-SEGURANCA
           INITIALIZE LINKAGE-ORMCOCBC
           MOVE '00360305.MES01'     TO LK-IDENT-ORIG-CBC
           MOVE '00360305.MES01'     TO LK-IDENT-DEST-CBC

           COMPUTE WS-TAM-MSG-EBCDIC = WS-TAM-MSG-CRIPTO
                                     - WS-TAM-CAB-SEGURANCA

      *    Trata ponteiros para a conversao
           EXEC CICS GETMAIN SET(LK-DEST-POINTER-CNV)
                         FLENGTH(WS-TAM-MSG-EBCDIC)
           END-EXEC
           SET ADDRESS OF WS-MSG-EBCDIC    TO LK-DEST-POINTER-CNV

           MOVE ALL  ' ' TO WS-MSG-EBCDIC

           MOVE  WS-MSG-CIFRADA(333:WS-TAM-MSG-EBCDIC) TO WS-MSG-EBCDIC
      *    Gera ID UNICO para simular a decriptografia
           PERFORM P0703-GERA-IDUNICO-CIFRAGEM
           .
      ******************************************************************
       P0701-PADRAOBROKER.
      *----------------------------------------------------------------*
           MOVE 1176                 TO WS-TAM-CAB-SEGURANCA
           INITIALIZE LINKAGE-ORMCOCBC
           MOVE '00360305.MES01'     TO LK-IDENT-ORIG-CBC
           MOVE '00360305.MES01'     TO LK-IDENT-DEST-CBC
      *    Atribuo o conteudo do ponteiro para a variavel de trabalho
           SET LK-MSG-ENT-CBC        TO ADDRESS OF WS-MSG-CIFRADA
           MOVE WS-TAM-MSG-CRIPTO    TO LK-TAM-MSG-ENT-CBC
           SET LK-MSG-SAI-CBC
                 TO ADDRESS OF WS-MSG-CIFRADA(1177:WS-TAM-MSG-CRIPTO)
           COMPUTE LK-TAM-MSG-SAI-CBC = LK-TAM-MSG-ENT-CBC
                                      - WS-TAM-CAB-SEGURANCA
      *    Endereco que ira receber as mensagens
           SET ADDRESS OF WS-MSG-DECRIPTO  TO LK-MSG-SAI-CBC
           COMPUTE WS-TAM-MSG-DECRIPTO = LK-TAM-MSG-SAI-CBC
      *    Gera ID UNICO para simular a decriptografia
           PERFORM P0703-GERA-IDUNICO-CIFRAGEM
      *    Entrega mensagem decriptografada
           PERFORM P0600-ENVIA-LOGSEG
      *    Convert a mensagem para EBCDIC
           PERFORM P0800-CONVERSAO-CARACT
           .
      ******************************************************************
       P0702-DECRIPTOGRAFIA.
      *----------------------------------------------------------------*
           INITIALIZE LINKAGE-ORMCOCBC
           MOVE 'D'                  TO LK-OPERACAO-CBC
           MOVE WS-TAM-MSG-CRIPTO    TO LK-TAM-MSG-ENT-CBC

      *    Atribuo o conteudo do ponteiro para a variavel de trabalho
           SET LK-MSG-ENT-CBC        TO ADDRESS OF WS-MSG-CIFRADA

           EXEC CICS LINK
                LINK PROGRAM('ORMCOCBC') COMMAREA(LINKAGE-ORMCOCBC)
                RESP(RESP1) RESP2(RESP2)
           END-EXEC

           IF ( (eibresp NOT EQUAL dfhresp(normal)) or
                (ErroGraveCBC) ) THEN
      *       Realizo rollback para devolver a mensagem para o MQ
              EXEC CICS SYNCPOINT ROLLBACK END-EXEC
              STRING
                  ' ORMNM996 -'
                  ' ****** ERRO GRAVE na decriptografia'
                  ' Rotina finalizada ' EIBTRNID
                  ' Favor contactar o suporte'
                  ' ' LK-DESC-ERRO-CBC
              DELIMITED BY SIZE INTO  LK-ASSUNTO-EMAIL-125
              END-STRING
              PERFORM P9200-EXE-ERRO
           END-IF
           .
      ******************************************************************
       P0703-GERA-IDUNICO-CIFRAGEM.
      *----------------------------------------------------------------*
           INITIALIZE LINKAGE-ORMCOUID

           EXEC CICS LINK
                LINK PROGRAM('ORMCOUID') COMMAREA(LINKAGE-ORMCOUID)
                RESP(RESP1) RESP2(RESP2)
           END-EXEC

           IF ( (eibresp NOT EQUAL dfhresp(normal)) or
                (LK-RETORNO-UID NOT EQUAL ZEROS)) THEN
      *       Realizo rollback para devolver a mensagem para o MQ
              EXEC CICS SYNCPOINT ROLLBACK END-EXEC
              STRING 'ORMNM996 -'
                     ' ****** ERRO GRAVE na GERACAO DO ID '
                     ' Rotina finalizada ' EIBTRNID
                     ' - Favor contactar o suporte ' LK-DE-ERRO-UID
                     DELIMITED BY SIZE INTO  LK-ASSUNTO-EMAIL-125
              END-STRING
              PERFORM P9200-EXE-ERRO
           END-IF

           MOVE LK-MSGID-UNICO-UID   TO LK-MSGID-UNICO-CBC
           MOVE LK-MSGID-UNICO-UID   TO WS-ID-UNICO-DECRI-MSG
           .
      ******************************************************************
       P0800-CONVERSAO-CARACT.
      **--------------------------------------------------------------**
      *    Calcula o tamanho da mensagem DEPOIS da conversao
      *
           COMPUTE WS-TAM-MSG-EBCDIC = WS-TAM-MSG-DECRIPTO / 2

           INITIALIZE LINKAGE-ORMCOCNV.
           MOVE 'U'                    TO LK-ORIG-FMT-CNV
           MOVE WS-TAM-MSG-DECRIPTO    TO LK-ORIG-TAMANHO-CNV
           SET LK-ORIG-POINTER-CNV     TO LK-MSG-SAI-CBC
           MOVE 'E'                    TO LK-DEST-FMT-CNV
           MOVE WS-TAM-MSG-EBCDIC      TO LK-DEST-TAMANHO-CNV

           EXEC CICS
                LINK PROGRAM('SFWCOCNV') COMMAREA(LINKAGE-ORMCOCNV)
                RESP(RESP1) RESP2(RESP2)
           END-EXEC

           IF ( (eibresp NOT EQUAL dfhresp(normal)) or
                (LK-RETURN-CODE-CNV NOT EQUAL ZEROS) ) THEN
      *       Realizar rollback para devolver a mensagem
              EXEC CICS SYNCPOINT ROLLBACK END-EXEC
              INITIALIZE LK-ASSUNTO-EMAIL-125
              STRING 'ORMNM996 - ERRO AO EXECUTAR O ORMCOCNV: '
                     LINKAGE-ORMCOCNV
                     DELIMITED BY SIZE INTO LK-ASSUNTO-EMAIL-125
              END-STRING
              MOVE LK-RETORNO-990        TO WS-RETORNO
              PERFORM P9200-EXE-ERRO
           END-IF
           .
      ******************************************************************
       P0900-RECUPERA-CODMSG.
      **--------------------------------------------------------------**
      *   Recupera o tipo da mensagem da mensagem define se ela eh
      *   uma mensagem que tem que ter garantia de entrega e tratamento
      *   ou uma mensagem que se não for tratada a mensa não gerarar
      *   maiores transtornos - no momento somente as mensagens DDA0110
      *   serão tratadas como sem garantia
      *   Quando existir novas mensagens com essa caracteristica uma
      *   solução seria colocar essa informaçao em UMA TS e comparar
      *   com o codigo da mensagem

           UNSTRING WS-MSG-EBCDIC(1:WS-TAM-MSG-EBCDIC)
             DELIMITED BY '<CodMsg>'
                       OR '</CodMsg>'
             INTO WS-FILLER-1 COUNT IN WS-FILLER-1-TAM
                  WS-CODMSG-REC COUNT IN WS-CODMSG-REC-TAM
                  WS-FILLER-2 COUNT IN WS-FILLER-2-TAM
           END-UNSTRING

           IF WS-FILLER-2-TAM EQUAL ZEROS THEN
      *      Realizar rollback para devolver a mensagem
              EXEC CICS SYNCPOINT ROLLBACK END-EXEC
              PERFORM P9200-EXE-ERRO
           END-IF

           MOVE WS-ORI-MSG-REC           TO WS-ORI-MSG-REC-ANT
           IF WS-CODMSG-REC(1:WS-CODMSG-REC-TAM) EQUAL 'GEN0004'
              MOVE 'R'                   TO WS-ORI-MSG-REC
           END-IF
           .
      ******************************************************************
       P1000-ABRIR-FILA.
      **--------------------------------------------------------------**
      *    Abre a fila solicitada
           MOVE ZEROS                    TO LK-RETORNO-990
           MOVE ZEROS                    TO LK-HCONN-990
           MOVE ZEROS                    TO LK-Q-HANDLE-990

           EXEC CICS
                LINK PROGRAM('ORMNM990') COMMAREA(LINKAGE-ORMPO990)
                RESP(RESP1) RESP2(RESP2)
           END-EXEC

           IF ( (eibresp NOT EQUAL dfhresp(normal)) or
                (LK-RETORNO-990 NOT EQUAL ZEROS) ) THEN
      *      Realizar rollback para devolver a mensagem
              EXEC CICS SYNCPOINT ROLLBACK END-EXEC
              INITIALIZE LK-ASSUNTO-EMAIL-125
              STRING 'ORMNM996 - ERRO AO EXECUTAR O ORMPO990: '
                     LINKAGE-ORMPO990
                     DELIMITED BY SIZE INTO LK-ASSUNTO-EMAIL-125
              END-STRING
              MOVE LK-RETORNO-990        TO WS-RETORNO
              PERFORM P9200-EXE-ERRO
           END-IF
           .
      ******************************************************************
       P1100-GET-FILA-LEITURA.
      **--------------------------------------------------------------**
           INITIALIZE LINKAGE-ORMPO991
           MOVE ZEROS                    TO LK-RETORNO-991
           MOVE 01                       TO LK-TAM-MIN-MSG-991
           MOVE WS-HCONN-LEITURA         TO LK-HCONN-991
           MOVE WS-Q-HANDLE-LEITURA      TO LK-Q-HANDLE-991
           MOVE WS-TP-TRIGGER-LEITURA    TO LK-TP-TRIGGER-991
           MOVE 'S'                      TO LK-WAIT-991
           MOVE ZEROS                    TO LK-WAITINTERVAL-991

           EXEC CICS
                LINK PROGRAM('ORMNM991') COMMAREA(LINKAGE-ORMPO991)
                RESP(RESP1) RESP2(RESP2)
           END-EXEC

           IF   (eibresp NOT EQUAL dfhresp(normal)) THEN
      *      Realizar rollback para devolver a mensagem
              EXEC CICS SYNCPOINT ROLLBACK END-EXEC
              INITIALIZE LK-ASSUNTO-EMAIL-125
              STRING 'ORMNM996 - ERRO AO EXECUTAR O ORMPO991: '
                     LINKAGE-ORMPO991
                     DELIMITED BY SIZE INTO LK-ASSUNTO-EMAIL-125
              END-STRING
              MOVE LK-RETORNO-990        TO WS-RETORNO
              PERFORM P9200-EXE-ERRO
           END-IF

           EVALUATE LK-RETORNO-991
             WHEN 0
                  SET ADDRESS OF WS-MSG-MQ-CRIPTO TO LK-MSG-PTR-991
             WHEN 2
                  STRING 'ORMNM996 - ERRO NA CHAMADA DO ORMPO991: '
                    'Erro no GET mensagem truncada retirada da fila - '
                    LINKAGE-ORMPO991
                    DELIMITED BY SIZE INTO  LK-ASSUNTO-EMAIL-125
                  END-STRING
                  PERFORM P9100-ENVIA-EMAIL
             WHEN 99999
                  CONTINUE
             WHEN OTHER
                  STRING 'ORMNM996 - ERRO NA CHAMADA DO ORMPO991: '
                    LINKAGE-ORMPO991
                    DELIMITED BY SIZE INTO  LK-ASSUNTO-EMAIL-125
                  END-STRING
      *           Realizo rollback para devolver a mensagem para o MQ
                  EXEC CICS SYNCPOINT ROLLBACK END-EXEC
                  MOVE LK-RETORNO-991    TO WS-RETORNO
                  PERFORM P9200-EXE-ERRO
           END-EVALUATE

           MOVE FUNCTION CURRENT-DATE    TO WS-CURRENT-DATA-HORA
           MOVE WS-CURRENT-DATA          TO WS-DATA-PUT-GET
           MOVE WS-CURRENT-HORA          TO WS-HORA-GET

           PERFORM P9000-GMT-TO-TIMEZONE
           IF WS-CURRENT-DATA NOT EQUAL ZEROS
              MOVE WS-CURRENT-HORA       TO WS-HORA-PUT
           ELSE
              MOVE WS-HORA-GET           TO WS-HORA-PUT
           END-IF

           MOVE WS-CURRENT-DTHR-FORMATADA TO WS-TS-DECRI-MSG
           .
      ******************************************************************
       P1200-PUT-MQ.
      **--------------------------------------------------------------**
      *    Coloca a mensagem na fila
           MOVE ZEROS                    TO LK-RETORNO-992
           MOVE 'N'                      TO LK-IC-LOGAR-MSG-992
           MOVE 'N'                      TO LK-IC-REPORT-992
           MOVE 'N'                      TO LK-IC-SYNCPOINT-992
           MOVE 2                        TO LK-MSGTYPE-992
           MOVE SPACES                   TO LK-TS-LOG-992

           EXEC CICS
                LINK PROGRAM('ORMNM992') COMMAREA(LINKAGE-ORMPO992)
                RESP(RESP1) RESP2(RESP2)
           END-EXEC

           IF ( (eibresp NOT EQUAL dfhresp(normal)) or
                (LK-RETORNO-992 NOT EQUAL ZEROS) ) THEN
      *       Realizar rollback para devolver a mensagem
              EXEC CICS SYNCPOINT ROLLBACK END-EXEC
              INITIALIZE LK-ASSUNTO-EMAIL-125
              STRING 'ORMNM996 - ERRO AO EXECUTAR O ORMPO992: '
                     LINKAGE-ORMPO992
                     DELIMITED BY SIZE INTO LK-ASSUNTO-EMAIL-125
              END-STRING
              MOVE LK-RETORNO-990        TO WS-RETORNO
              PERFORM P9200-EXE-ERRO
           END-IF
           .
      ******************************************************************
       P1300-FREE-MAIN.
      **--------------------------------------------------------------**
           PERFORM P1310-FREE-MAIN-MSG
           EXEC CICS
                FREEMAIN DATAPOINTER(LK-MSG-PTR-992)
                RESP(RESP1) RESP2(RESP2)
                NOHANDLE
           END-EXEC
           EXEC CICS
                FREEMAIN DATAPOINTER(LK-DEST-POINTER-CNV)
                RESP(RESP1) RESP2(RESP2)
                NOHANDLE
           END-EXEC
           .
      ******************************************************************
       P1310-FREE-MAIN-MSG.
      **--------------------------------------------------------------**
           EXEC CICS
                FREEMAIN DATAPOINTER(LK-MSG-PTR-991)
                RESP(RESP1) RESP2(RESP2)
                NOHANDLE
           END-EXEC
           EXEC CICS
                FREEMAIN DATAPOINTER(LK-MSG-SAI-CBC)
                RESP(RESP1) RESP2(RESP2)
                NOHANDLE
           END-EXEC
           .
      ******************************************************************
       P9000-GMT-TO-TIMEZONE.
      **--------------------------------------------------------------**
           MOVE LK-PUTDATE-991           TO WS-CURRENT-DATA
           MOVE LK-PUTTIME-991           TO WS-CURRENT-HORA

      * OBS: Resolver para horário de verão
           IF WS-CURRENT-HR >= 3
              COMPUTE WS-CURRENT-HR = WS-CURRENT-HR - 3
           ELSE
      * Verifico data do MQ válida
              IF LK-PUTDATE-991 >= 16010101
              AND LK-PUTDATE-991 <= 99991231
                 COMPUTE WS-CURRENT-HR = 24 - 3 + WS-CURRENT-HR
                 MOVE LK-PUTDATE-991     TO WS-DT-IN
                 COMPUTE WS-DT-INTEGER =
                         FUNCTION INTEGER-OF-DATE (WS-DT-IN) - 1
                 COMPUTE WS-DT-IN      =
                         FUNCTION DATE-OF-INTEGER (WS-DT-INTEGER)
                 MOVE WS-DT-IN           TO WS-CURRENT-DATA
              ELSE
                 MOVE ZEROS              TO WS-CURRENT-DATA
              END-IF
           END-IF

           STRING WS-CURRENT-AA '-'
                  WS-CURRENT-MM '-'
                  WS-CURRENT-DD '-'
                  WS-CURRENT-HR '.'
                  WS-CURRENT-MN '.'
                  WS-CURRENT-SS '.'
                  WS-CURRENT-MS '0000'
                  DELIMITED BY SIZE INTO WS-CURRENT-DTHR-FORMATADA
           END-STRING
           .
      ******************************************************************
       P9000-ABEND.
      **--------------------------------------------------------------**
      * Recuperar o Codigo do Abend e o Prg
           EXEC CICS ASSIGN
                ABCODE (WS-ABEND-CODE)
                ABPROGRAM(WS-ABEND-PROGRAM)
           END-EXEC

           IF (WS-ABEND-PROGRAM = LOW-VALUES) THEN
              MOVE SPACES TO WS-ABEND-PROGRAM
           END-IF

      * Desfazer tudo que já foi feito
           EXEC CICS SYNCPOINT ROLLBACK END-EXEC

      * Montar texto para informar o problema
           INITIALIZE LK-ASSUNTO-EMAIL-125
           STRING 'TRANSACAO: ' EIBTRNID
                  ' - ORMNM996 - Término anormal '
                  WS-ABEND-PROGRAM
                  ' com o código ' WS-ABEND-CODE '. '
                  ' DataHora: ' WS-CURRENT-DATE-FIELDS-I
                  DELIMITED BY SIZE
                  INTO LK-ASSUNTO-EMAIL-125
           END-STRING

      * Enviar e-mail
           PERFORM P9100-ENVIA-EMAIL

           PERFORM P1300-FREE-MAIN

      * Finalizar o programa
           EXEC CICS RETURN END-EXEC
           .
      ******************************************************************
       P9100-ENVIA-EMAIL.
      **--------------------------------------------------------------**
           MOVE ZEROS                    TO LK-RETORNO-125
           MOVE 'CEDESBR911@MAIL.CAIXA'  TO LK-EMAIL-DES-125
           MOVE 'ORMTS001'               TO LK-TS-CONTEUDO-125

           EXEC CICS                                                    13800
                LINK PROGRAM('ORMPO125') COMMAREA(LINKAGE-ORMPO125)     013900
                RESP(RESP1) RESP2(RESP2)
           END-EXEC                                                     14000
           .
      ******************************************************************
       P9200-EXE-ERRO.
      **--------------------------------------------------------------**
           PERFORM P9100-ENVIA-EMAIL
           PERFORM P1300-FREE-MAIN

           INITIALIZE LK-ASSUNTO-EMAIL-125

      *    Recupera o nome do CICS
           EXEC CICS ASSIGN SYSID(WS-SYSID) END-EXEC

           STRING 'ORMNM996 ERRO DE PROGRAMA'
                  ' - TRANSACAO: ' EIBTRNID
                  ' - CICS: ' WS-SYSID
                  ' - EIBRESP: ' WS-RETORNO
                  DELIMITED BY SIZE INTO LK-ASSUNTO-EMAIL-125
           END-STRING

           PERFORM P9100-ENVIA-EMAIL
           EXEC CICS RETURN END-EXEC
           .