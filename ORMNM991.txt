 CBL NODYNAM,LIB,OBJECT,RENT,RES,APOST
      ******************************************************************
      *           SIORM - SISTEMA ORQUESTRADOR DE MENSAGENS            *
      *----------------------------------------------------------------*
      * Objetivo   : Realizar get em fila local no MQ                  *
      *                                                                *
      ******************************************************************
       IDENTIFICATION DIVISION.
       PROGRAM-ID. ORMNM991.
       AUTHOR. P979322.
      ******************************************************************
       ENVIRONMENT DIVISION.
       CONFIGURATION SECTION.
       SOURCE-COMPUTER. IBM-3090.
       SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA.
      ******************************************************************
       DATA DIVISION.
       WORKING-STORAGE SECTION.
      *----------------------------------------------------------------*
       77 WS-RESP1                       PIC 9(05) VALUE ZEROS.
       77 WS-RESP2                       PIC 9(05) VALUE ZEROS.
       77 WS-TAM-XML                     PIC S9(9)
                                         BINARY VALUE +2162688.
      **  DECLARE MQI STRUCTURES NEEDED
      * MQI NAMED CONSTANTS
       01 FILLER.
          COPY 'CMQV'.
      * MESSAGE DESCRIPTOR
       01 MESSAGE-DESCRIPTOR.
          COPY 'CMQMDV'.
      * GET MESSAGE OPTIONS
       01 GMOPTIONS.
          COPY 'CMQGMOV'.
      ** NOTE, SAMPLE USES DEFAULTS WHERE IT CAN
       01 WS-HCONN                       PIC S9(9) BINARY.
       01 WS-Q-HANDLE                    PIC S9(9) BINARY.
       01 WS-REASON                      PIC S9(9) BINARY.
       01 WS-REASON-D                    PIC 9(06) VALUE ZEROS.
       01 WS-MSG-VALIDA                  PIC X(01) VALUE SPACES.
       01 WS-TAM-GETMAIN-PT              PIC S9(8) COMP VALUE +0.
       01 WS-COMPLETION-CODE             PIC S9(9) BINARY.
       01 WS-BUFFER-LENGTH               PIC S9(9) BINARY VALUE +0.
      *----------------------------------------------------------------*
       COPY ORMCP125.

      ******************************************************************
       LINKAGE SECTION.
      *----------------------------------------------------------------*
       01 DFHCOMMAREA.
          03 LK-RETORNO                  PIC 9(05).
          03 LK-TAM-MIN-MSG              PIC 9(07).
          03 LK-HCONN                    PIC S9(9) BINARY.
          03 LK-Q-HANDLE                 PIC S9(9) BINARY.
          03 LK-MSGID                    PIC X(24).
          03 LK-CORRELID                 PIC X(24).
          03 LK-TP-TRIGGER               PIC X(01).
      * N-None F-First E-Every D=Depth
          03 LK-WAIT                     PIC X(01).
      * S-Sim ou N-Não
          03 LK-WAITINTERVAL             PIC 9(09).
      * Em milisegundos
      * Se waitinterval igual 999999999 fica em loop eterno
          03 LK-PUTDATE                  PIC X(08).
          03 LK-PUTTIME                  PIC X(08).
          03 LK-MSG-PTR                  POINTER.
          03 LK-TAM-MAX-MSG              PIC 9(07).
      *
       01 WS-MSG-GET.
          03 WS-DATA-LENGTH              PIC S9(9) BINARY VALUE +1.
          03 WS-BUFFER.
             05 FILLER PIC X
                OCCURS 1 TO 2162688 DEPENDING ON WS-DATA-LENGTH.
      ******************************************************************
       PROCEDURE DIVISION USING DFHEIBLK DFHCOMMAREA.
      *----------------------------------------------------------------*
           PERFORM P0000-INICIALIZA-VARIAVEIS
           PERFORM P0100-GET-MQ
           PERFORM P0800-TRANS-VAR-LINKAGE

           EXEC CICS RETURN END-EXEC
           .
      ******************************************************************
       P0000-INICIALIZA-VARIAVEIS.
      *----------------------------------------------------------------*
           MOVE ZEROS                    TO LK-RETORNO

           IF LK-TAM-MAX-MSG IS NUMERIC
              MOVE LK-TAM-MAX-MSG        TO WS-BUFFER-LENGTH
           ELSE
              MOVE +1081344              TO WS-BUFFER-LENGTH
           END-IF

           PERFORM P0010-COLOCA-MSG-PTR
           .
      ******************************************************************
       P0010-COLOCA-MSG-PTR.
      *----------------------------------------------------------------*
           COMPUTE WS-TAM-GETMAIN-PT   = WS-TAM-XML
                                       + LENGTH OF WS-DATA-LENGTH

           EXEC CICS GETMAIN
                     SET(LK-MSG-PTR)
                     FLENGTH(WS-TAM-GETMAIN-PT)
                     RESP(WS-RESP1) RESP2(WS-RESP2)
           END-EXEC

           SET ADDRESS OF WS-MSG-GET   TO LK-MSG-PTR
           .
      ******************************************************************
       P0100-GET-MQ.
      *----------------------------------------------------------------*
           PERFORM UNTIL WS-MSG-VALIDA = 'S'

             PERFORM P0110-INICIALIZA-GET
             PERFORM P0120-LER-MENSAGEM

           END-PERFORM
           .
      ******************************************************************
       P0110-INICIALIZA-GET.
      *----------------------------------------------------------------*
           MOVE ZEROS                    TO MQGMO-OPTIONS

           IF LK-WAIT EQUAL 'S'
              MOVE LK-WAITINTERVAL       TO MQGMO-WAITINTERVAL
              COMPUTE MQGMO-OPTIONS = MQGMO-OPTIONS
                                    + MQGMO-ACCEPT-TRUNCATED-MSG
                                    + MQGMO-WAIT
           ELSE
              COMPUTE MQGMO-OPTIONS = MQGMO-OPTIONS
                                    + MQGMO-ACCEPT-TRUNCATED-MSG
           END-IF

      * Se informar o MSGID o GET será feito pelo seu valor
           IF LK-MSGID NOT EQUAL SPACES
              MOVE LK-MSGID              TO MQMD-MSGID
           ELSE
              MOVE MQMI-NONE             TO MQMD-MSGID
           END-IF

           MOVE MQCI-NONE                TO MQMD-CORRELID
           MOVE LK-HCONN                 TO WS-HCONN
           MOVE LK-Q-HANDLE              TO WS-Q-HANDLE
           .
      ******************************************************************
       P0120-LER-MENSAGEM.
      *----------------------------------------------------------------*
           MOVE 'S'                      TO WS-MSG-VALIDA

           CALL 'MQGET' USING WS-HCONN
                            , WS-Q-HANDLE
                            , MESSAGE-DESCRIPTOR
                            , GMOPTIONS
                            , WS-BUFFER-LENGTH
                            , WS-BUFFER
                            , WS-DATA-LENGTH
                            , WS-COMPLETION-CODE
                            , WS-REASON
           END-CALL

           PERFORM P0200-VERIFICA-ERROS-MQ

           IF (WS-REASON EQUAL MQRC-NO-MSG-AVAILABLE)
              AND (LK-WAITINTERVAL NOT EQUAL 999999999) THEN
              MOVE 'S'                   TO WS-MSG-VALIDA
              MOVE 99999                 TO LK-RETORNO
           ELSE
              IF MQMD-MSGTYPE = MQMT-REPORT
                 PERFORM P0300-VER-REPORT
              ELSE
                 PERFORM P0400-VER-TAMANHO
              END-IF
           END-IF
           .
      ******************************************************************
       P0200-VERIFICA-ERROS-MQ.
      *----------------------------------------------------------------*
           IF WS-REASON IS NOT EQUAL MQRC-NONE
           AND WS-REASON IS NOT EQUAL MQRC-NO-MSG-AVAILABLE
              PERFORM P0201-MQ-ERRO
      *       Se erro 2 a mensagem esta truncada, mas continuo
      *       a tratar pq vou tentar descobrir a origem da mensagem
              IF LK-RETORNO NOT EQUAL 2
                 PERFORM P9200-EXE-ERRO
              ELSE
                 PERFORM P9000-ENVIA-EMAIL
              END-IF
           END-IF
           .
      ******************************************************************
       P0201-MQ-ERRO.
      *----------------------------------------------------------------*
           MOVE WS-REASON                TO WS-REASON-D

           IF WS-REASON EQUAL 2079
              MOVE 02                    TO LK-RETORNO
           ELSE
              MOVE 01                    TO LK-RETORNO
           END-IF

           STRING 'MQ GET ERRO -'
                  ' ORMNM991 - Erro: ' WS-REASON-D
               DELIMITED BY SIZE INTO LK-ASSUNTO-EMAIL-125
           END-STRING
           .
      ******************************************************************
       P0300-VER-REPORT.
      *----------------------------------------------------------------*
           IF  (MQMD-CORRELID NOT EQUAL SPACES)
              PERFORM P0500-INC-REPORT
           END-IF
           .
      ******************************************************************
       P0400-VER-TAMANHO.
      *----------------------------------------------------------------*
           MOVE 'S'                      TO WS-MSG-VALIDA

           IF (WS-DATA-LENGTH < LK-TAM-MIN-MSG)
              PERFORM P0410-MONTA-MSG-ERRO-TAMANHO
           END-IF
           .
      ******************************************************************
       P0410-MONTA-MSG-ERRO-TAMANHO.
      *----------------------------------------------------------------*
           STRING 'MQ GET ERRO - '
                  'ORMNM991 - Mensagem menor que o tamanho '
                  'minimo definido.'
                  'Tamanho Minimo  : ' LK-TAM-MIN-MSG
                  DELIMITED BY SIZE INTO LK-ASSUNTO-EMAIL-125
           END-STRING

           PERFORM P9000-ENVIA-EMAIL

           IF LK-TP-TRIGGER EQUAL 'E'
              MOVE 'S'                   TO WS-MSG-VALIDA
              MOVE 99999                 TO LK-RETORNO
      *    Para outros tipos de TRIGGER, não saio do loop
           ELSE
              MOVE 'N'                   TO WS-MSG-VALIDA
           END-IF
           .
      ******************************************************************
       P0500-INC-REPORT.
      *----------------------------------------------------------------*
           STRING 'MQ REPORT- '
                  'ORMNM991 - Report não pode ser tratado'
                  ' (MSGID: ' MQMD-MSGID
                  ' /CORRELID: ' MQMD-CORRELID ')'
                  DELIMITED BY SIZE INTO LK-ASSUNTO-EMAIL-125
           END-STRING

           PERFORM P9000-ENVIA-EMAIL

           IF LK-TP-TRIGGER EQUAL 'E'
              MOVE 'S'                   TO WS-MSG-VALIDA
              MOVE 99999                 TO LK-RETORNO
      *    Para outros tipos de TRIGGER, não saio do loop
           ELSE
              MOVE 'N'                   TO WS-MSG-VALIDA
           END-IF
           .
      ******************************************************************
       P0800-TRANS-VAR-LINKAGE.
      *----------------------------------------------------------------*
           MOVE MQMD-MSGID               TO LK-MSGID
           MOVE MQMD-CORRELID            TO LK-CORRELID
           MOVE MQMD-PUTDATE             TO LK-PUTDATE
           MOVE MQMD-PUTTIME             TO LK-PUTTIME
           .
      ******************************************************************
       P9000-ENVIA-EMAIL.
      *----------------------------------------------------------------*
           MOVE ZEROS                    TO LK-RETORNO-125
           MOVE 'cedesbr911@mail.caixa'  TO LK-EMAIL-DES-125
           MOVE 'ORMTS001'               TO LK-TS-CONTEUDO-125

           EXEC CICS
                LINK PROGRAM('ORMPO125') COMMAREA(LINKAGE-ORMPO125)
                RESP(WS-RESP1) RESP2(WS-RESP2)
           END-EXEC
           .
      ******************************************************************
       P9200-EXE-ERRO.
      *----------------------------------------------------------------*
           STRING 'ERRO ORMNM991: ' LK-RETORNO
                  ' - LINKAGE-991: ' DFHCOMMAREA
                  DELIMITED BY SIZE INTO LK-ASSUNTO-EMAIL-125
           END-STRING

           PERFORM P9000-ENVIA-EMAIL

           EXEC CICS RETURN END-EXEC
           .